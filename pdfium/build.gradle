apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.github.zoltaneusz'
version = '1.10.0'

android {
    namespace "com.shockwave.pdfium"
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName "1.10.0"
        externalNativeBuild {
            ndkBuild {
                arguments "APP_PLATFORM=android-24"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    ndkVersion "27.3.13750724"

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jni.srcDirs = ['libs']
            jniLibs.srcDir 'src/main/jniLibs'
        }
    }
}

repositories {
    google()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'androidx.annotation:annotation:1.9.0'
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.zoltaneusz'
                artifactId = 'pdfium-android'
                version = '1.10.0'
            }
        }
        repositories {
            mavenLocal()
            maven { url 'https://jitpack.io' }
        }
    }
//    tasks.getByPath(':assembleRelease').doLast {
//        copy {
//            from("$buildDir/intermediates/merged_native_libs/release/out/lib/")
//            into("$buildDir/outputs/aar/jni/")
//        }
//    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'publishReleasePublicationToMavenLocal') {
        task.dependsOn('build')
    }
}
